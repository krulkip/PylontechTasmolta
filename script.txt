>D 64
;variablen für berechnungen
res=0
rstr=""
label=""
test=""
lraw=""
val=0
val2=""
batno=0
dbatno=0
volt=0
curr=0
temp=0
coul=0
Timer=0
Text1="Battery"
is1=""
is2=""

>B
res=0
res=so(3 1 115200 8N1 1024)
;print open %res%


>F
res=sa()
if res>0 {
   rstr=sr(10)
   ;print rstr=%rstr%
   rstr=sb(rstr 1 sl(rstr))
   label=sb(rstr 0 9)
   lraw=sb(rstr 0 13)
   ;print %lraw%:
   val=st(rstr : 2)
   if val==0 {
       val2=st(rstr : 2)
   }

   switch label
   case "Voltage  "
      volt=val/1000
   case "Temperatu"
      temp=val/1000
   case "Coulomb  "
      coul=val
   case "Current  "
      curr=val/1000
   case "Basic Sta"
      is1=val2
   case "Soh. Stat"
      is2=val2
   ends
}

>S
;Executed every second
if upsecs%5==0
then
res=sw("pwr 1\n")
;print write cmd
endif
;print %volt% %curr% %temp% %coul%
Timer+=1
;print Timer %Timer%
if Timer==10
then
->DisplayText [z]
->DisplayText [l1c1]%Text1%
->DisplayText [l2c1]Volt %1volt%V
->DisplayText [l1c17]Curr %1curr%A
->DisplayText [l2c17]Temp %1temp%~DFC
->DisplayText [l1c9]SOC %0coul%%%
test=sb(is1 1 4)
->DisplayText [l2c13]%test%
test=sb(is2 1 4)
->DisplayText [l1c29]%test%
endif
if Timer>20
then
Timer=0
endif

 


>T
; send mqtt individualtopics also/instead json see below in J
=>publish iot/%topic%/Voltage %4volt%
=>publish iot/%topic%/Current %0curr%
=>publish iot/%topic%/Coulomb %0coul%

>W
Voltage{m}%4volt% V
Current{m}%1curr% A
Temperature{m}%1temp% °C
SOC{m}%0coul% %%
Status{m}%is1%
SOH Status{m}%is2%

>J
,"pylon":{"Voltage":%4volt%,"Current":%0curr%,"Coulomb":%0coul%,"Temperature":%3temp%}
#
